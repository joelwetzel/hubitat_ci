plugins {
    id 'groovy'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier = 'groovydoc'
    from groovydoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'me.biocomp.hubitat_ci'
            artifactId = 'hubitat_ci'
            version = 0.48

            from components.java

            artifact sourcesJar
            artifact groovydocJar
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY") ?: "biocomp/hubitat_ci"}")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

dependencies {
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'org.codehaus.groovy:groovy-all:2.5.4'
    implementation 'org.codehaus.groovy:groovy-dateutil:2.5.4'
    implementation 'org.ow2.asm:asm:7.0'
    implementation 'org.quartz-scheduler:quartz:2.3.0'

    implementation 'su.litvak.chromecast:api-v2:0.11.1'

    implementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.5.0'

    implementation "org.spockframework:spock-core:1.2-groovy-2.5"
    testImplementation 'junit:junit:4.12'
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '3.3.0'
}
